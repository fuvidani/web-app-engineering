buildscript {
    ext.kotlin_version = '1.2.30'
    ext.spring_version = '2.0.0.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}
plugins {
    id 'com.sourcemuse.mongo' version '1.0.6'
}

version = '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: "kotlin-allopen"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.19.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile 'org.springframework.boot:spring-boot-starter-webflux'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.5'
    compile 'io.jsonwebtoken:jjwt:0.9.0'
    compile 'com.google.code.gson:gson:2.8.2'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.9.Final'
    compile group: 'org.glassfish', name: 'javax.el', version: '3.0.0'
    compile group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    compile group: 'io.projectreactor', name: 'reactor-test', version: '3.1.6.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.3'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    runWithMongoDb = true
}

mongo {
    port = 'RANDOM'
    logging = 'console'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    //noinspection GroovyAssignabilityCheck
    args "src/**/*.kt", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    //noinspection GroovyAssignabilityCheck
    args "-F", "src/**/*.kt"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn jacocoTestReport